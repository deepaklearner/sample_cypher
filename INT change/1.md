UNWIND rows AS row
WITH row 
WHERE row.AetnaIdentifier_flag = "y"

MATCH (usr:User {employeeNumber: row.CVSResourceid})
WITH usr 
WHERE NOT (usr)-[]-(:AetnaNetworkIdentifier:NetworkIdentifier) 
WITH COLLECT(usr) AS user_array

MATCH (aetna_identifier:AetnaNetworkIdentifier:NetworkIdentifier) 
WHERE NOT (aetna_identifier) - []-() 
WITH aetna_identifier, user_array 
ORDER BY aetna_identifier.networkid ASC 

WITH COLLECT(aetna_identifier) AS id_array, user_array
UNWIND apoc.coll.zip(user_array, id_array) AS row 

WITH 
    row[0] AS u, 
    row[1] AS aetna_identifier, 
    CASE 
        WHEN 'Employee' IN labels(row[0]) THEN 'A' 
        WHEN 'Contractor' IN labels(row[0]) THEN 'N' 
    END AS prefix

MERGE (u)-[:HAS_AETNA_ID {assigned_date: localdatetime()}]-(aetna_identifier)
SET 
    aetna_identifier.networkid = prefix + aetna_identifier.uid,
    u.aetnaresourceid = prefix + aetna_identifier.uid

FOREACH (x IN CASE WHEN u.cvsnetworkid IS NULL THEN [1] END | 
    SET u.cvsnetworkid = aetna_identifier.networkid
)

RETURN COUNT(*) AS total
